package drools.spring.rules;

import drools.spring.example.facts.Symptom;
import drools.spring.example.facts.Diagnose;
import drools.spring.example.facts.DiagnoseMedicament;
import drools.spring.example.facts.DiagnoseSymptom;
import drools.spring.example.facts.Illness;

global Long pId;

rule "Patient diagnosed with illness which has symptom high temp in last 14 days"
	salience 50
	agenda-group "set-symptom-num"
	no-loop 
	lock-on-active true
	when
		$illness: Illness( name == "Akutna bubrezna povreda" && $numOfSymptoms: symptomsFound &&
			$numOfSpecSymptoms: specificSymptomsFound && $sym: symptomTermsFound )
		$s: Symptom(Symptom.Term.SUFFERED_FROM_ILLNESS_WITH_SYMPTOM_HIGH_TEMPERATURE_LAST_2_WEEKS not memberOf $sym )
		$d: Diagnose( $date: date && patientId == pId && $diagnoseId: id 
			&& $date >= (addDays(-14)) )
		$dSym: DiagnoseSymptom( symptomTerm == "TEMP_OVER_38" || symptomTerm == "TEMP_BETWEEN_40_AND_41" 
			&& diagnoseId == $diagnoseId )	
	then
		$illness.setSymptomsFound($numOfSymptoms + 1);
		$illness.setSpecificSymptomsFound($numOfSpecSymptoms + 1);
		$illness.getSymptomTermsFound().add(Symptom.Term.SUFFERED_FROM_ILLNESS_WITH_SYMPTOM_HIGH_TEMPERATURE_LAST_2_WEEKS);
		update($illness);
end

rule "Patient recovers from surgery"
	salience 50
	agenda-group "set-symptom-num"
	no-loop 
	lock-on-active true
	when
		$illness: Illness( name == "Akutna bubrezna povreda" && $numOfSymptoms: symptomsFound &&
			$numOfSpecSymptoms: specificSymptomsFound )
		$symptom: Symptom( term == Symptom.Term.SURGERY_RECOVERY )
	then
		$illness.setSymptomsFound($numOfSymptoms + 1);
		$illness.setSpecificSymptomsFound($numOfSpecSymptoms + 1);
		update($illness);
end

rule "Patient used antibiotics in last 21 days"
	salience 50
	agenda-group "set-symptom-num"
	no-loop 
	lock-on-active 
	when
		$illness: Illness( name == "Akutna bubrezna povreda" && $numOfSymptoms: symptomsFound &&
			$numOfSpecSymptoms: specificSymptomsFound && $sym: symptomTermsFound )
		$s: Symptom( Symptom.Term.SUFFERED_AND_USED_ANTIBIOTICS_LAST_3_WEEKS not memberOf $sym )
		$d1: Diagnose( $idD: id && $d: date && $d >= (addDays(-21)) && patientId == pId )
		$diagnoseMed: DiagnoseMedicament( diagnoseId == $idD && medicamentCategory == "ANTIBIOTICS" )
	then
		$illness.setSymptomsFound($numOfSymptoms + 1);
		$illness.setSpecificSymptomsFound($numOfSpecSymptoms + 1);
		$illness.getSymptomTermsFound().add(Symptom.Term.SUFFERED_AND_USED_ANTIBIOTICS_LAST_3_WEEKS);
		update($illness);
end